//===== eAthena Script =======================================
//= Novice's Guild Castles War of Emperium Usher NPC
//===== By: ==================================================
//= Lupus
//===== Current Version: =====================================
//= 1.7
//===== Compatible With: =====================================
//= eAthena 1+ with eaclass(); RO Episode 4+
//===== Description: =========================================
//=
//===== Additional Comments: =================================
//= Now you have access to 4 N Guild castles. They don't have
//= dungeons. And 2nd Classes can't seize these Castles.
//= These new castles need a new strategy. It would bring some
//= fun and live to your game.
//= NOTE: If your Guild Master is 2nd class, then he could 
//=	rule the Castles and gather Treasure Boxes after WoE
//= 1.1 Now 2nd classes can't enter NC place at all
//= 1.2 Restricted access of SG/SL. On warp clear some 
//=	unallowed buffs [Lupus]
//= 1.3 Restricted TK,High classes >=90 BaseLevel,
//= 	updated list of unallowed buffs [Lupus]
//= 1.4 Restricted it to 80 Base Level [Lupus]
//= 1.5 According to official info: 1 Treasure Chest per Castle
//= 1.6 Only 1st Class < 60 BaseLevel Players can participate
//= 1.7 Guilds with Emergency Call or with 9+ skill points
//=		can't take part in NWoE. [Lupus]
//=	TODO: The official entrance is in Izlude.
//============================================================


prontera,146,163,6	script	初學者城堡	729,{
	mes "[西塔]";
	if(getcharid(2)!=0){
		if(getgdskilllv(getcharid(2),10013)){
			mes "我明白了... 你的公會有緊急呼叫技能";
			mes "你不能進入初學者公會區域";
			emotion e_hmm;
			emotion e_wah,1;
			close;
		}
		if(
			getgdskilllv(getcharid(2),10000) +
			getgdskilllv(getcharid(2),10001) +
			getgdskilllv(getcharid(2),10002) +
			getgdskilllv(getcharid(2),10003) +
			getgdskilllv(getcharid(2),10004) +
			getgdskilllv(getcharid(2),10005) +
			getgdskilllv(getcharid(2),10006) +
			getgdskilllv(getcharid(2),10007) +
			getgdskilllv(getcharid(2),10008) +
			getgdskilllv(getcharid(2),10009) +
			getgdskilllv(getcharid(2),10010) +
			getgdskilllv(getcharid(2),10011) +
			getgdskilllv(getcharid(2),10012) +
			getgdskilllv(getcharid(2),10013) +
			getgdskilllv(getcharid(2),10014) > 9
		){
			mes "我明白了... 你的公會有緊急呼叫技能";
			mes "你不能進入初學者公會區域";
			emotion e_hmm;
			emotion e_wah,1;
			close;
		}
	} else {
		mes "^FF0000你必須先加入公會才能打公會石!^000000";
	}
	mes "嗨! 我是新的初學者城堡接待員";
	next;
	if( !(eaclass()&(EAJL_2|EAJL_UPPER)) && BaseLevel<60 )
		menu "傳送到初學者城堡",M_WARP,"取消",-;

	mes "[西塔]";
	mes "很抱歉，你無法進入初學者公會";
	emotion e_sry;
	close;

M_WARP:
	//remove several unallowed buffs
	sc_end SC_ASSUMPTIO;
	sc_end SC_IMPOSITIO;
	sc_end SC_SUFFRAGIUM;
	sc_end SC_MAGNIFICAT;
	sc_end SC_WEAPONPERFECTION;
	sc_end SC_GOSPEL;
	sc_end SC_BASILICA;
	sc_end SC_MAGICPOWER;
	sc_end SC_MARIONETTE;
	sc_end SC_MARIONETTE2;
	sc_end SC_DEVOTION;
	sc_end SC_SACRIFICE;
	sc_end SC_MAXOVERTHRUST;
	sc_end SC_SPIRIT;
	warp "n_castle",102,93+rand(14);
	close;
}


n_castle,102,107,5	script	西塔	729,{
	mes "[西塔]";
	mes "你好，"+ strcharinfo(0) +"需要幫忙嗎?";
	next;
	menu "傳送到普隆德拉!",-,"取消",LEnd;
	
		warp "prontera",155,177+rand(5);
		close;
	LEnd:
		mes "[西塔]";
		mes "好的。";
		close;
}